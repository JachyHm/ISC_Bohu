show -1
timeout=0
mtimeout=500

;vychozi hodnoty, pokud nejsou definovane v predpisu jinak
ato = 0
logdir = ""
rychlost = 115200

;--- OKNO VYBERU PALICIHO PREDPISU
filenamebox "Vyber pálící pøedpis"
if result <> 0 then
    include inputstr
else
    messagebox 'Nebyl vybrán žádný soubor, nahrávání ukonèeno' 'Chyba'
    end
endif
;---
connect ""
settitle verze

messagebox 'Pøipoj nahrávací kabel do karty AVV a stiskni OK' 'Nahrávání AVV'
for indexport 1 99
    int2str jmport indexport
    seriport = "/C="
    strconcat seriport jmport
    connect seriport
    setbaud rychlost
    testlink
    if result<>2 goto pokrcom ;pokud nelze COM otevrit, neni AVV

    sendbreak
    pause 1
    send #13

    wait 'Heslo:' 'AVV>' 'Zaznam dat pripraven'
    if result=1 goto zadejheslo
    if result=2 goto naselcom
    if result=3 goto prihlaseni
    ;pokud se nic neozyva, neni AVV

    :pokrcom
    disconnect 0
    mpause 300
next

messagebox 'Nenalezen žádný COM port s AVV, zkontrolujte COM' 'Chyba'
end

:prihlaseni
mpause 300
send #13
mpause 300
;pri pomalych bd nelze prenest z nejakeho duvodu heslo jako sendln -> posila se postupne po znacich
:zadejheslo
strmatch heslo '(.{1})(.{1})(.{1})(.{1})(.{1})(.{1})'
send groupmatchstr1
mpause 100
send groupmatchstr2
mpause 100
send groupmatchstr3
mpause 100
send groupmatchstr4
mpause 100
send groupmatchstr5
mpause 100
send groupmatchstr6
mpause 100
send #13

;pokud po zadani hesla AVV neodpovi AVV>, nezadali jsme spravne heslo
wait 'AVV>'
if result<>1 then
    messagebox 'Nesprávné heslo!' 'Chyba'
    end
endif

:naselcom
vypisport = "Nalezen COM "     
strconcat vypisport jmport    
messagebox vypisport "Sériový port"

strlen jmport        
if result=1 then
    strinsert jmport 1 '0'
endif

;----- Mazani stranek -----

for indexparu 0 15 
    int2str sudastr (2*indexparu)
    testmaz = "mazani"
    prikmaz = "e"
    dotazradek = "Smazat stranku "
    strlen sudastr        
    if result=1 then
        strconcat dotazradek "0"
    endif
    strconcat dotazradek sudastr
    strconcat dotazradek " [A/N] ?"
    strconcat prikmaz sudastr
    if mazani[indexparu]=1 then 
        send prikmaz #13
        wait dotazradek
        if result=1 sendln 'A'
        wait 'Smazat, Obnovit (+ Ulozit), Konec [S/O/U/K] ?' 'Stranky smazany O.K.'
        if result=0 goto pokrmaz
        if result=1 sendln 'S'
        if result=2 goto pokrmaz
        wait 'Stranky smazany O.K.'
    endif
    :pokrmaz
next

beep 0
send #13
mpause 500

;--- Paleni stranek ---
getdir aktdir
strlen aktdir
if result<>1 then
    strconcat aktdir '\'
endif
for indexstran 0 31
    int2str strstran indexstran
    prikpal = "p"
    dotazradek = "Zapsat soubor do stranky "
    strlen strstran        
    if result=1 then
        strconcat dotazradek "0"     
    endif
    strconcat dotazradek strstran
    strconcat dotazradek " [A/N] ?"
    strconcat prikpal strstran
    absolutni=soubor[indexstran]
    strlen absolutni
    if result<=1 goto pokrnahr
    strmatch absolutni '([A-z]:){0,1}\\'
    if result<>1 then
        strinsert absolutni 1 aktdir
    endif
    filesearch absolutni
    if result<>0 then
        send prikpal #13
        mpause 300
        ;stranka na kterou se nahrava nemusi existovat, pak se Kermitem stejne snazi data procpat
        ;vetsinou konci absolutni cinskou polevkou z AVV
        kmtsend absolutni
        wait dotazradek
        if result=1 sendln 'A'
        wait 'Data se shoduji.'
    else
        chyba = 'Soubor s daty "'
        strconcat chyba absolutni
        strconcat chyba '" nebyl nalezen!'
        messagebox chyba "Chyba"
    endif
    :pokrnahr
next

wait 'AVV>'

if ato<>1 then
    yesnobox 'Chcete zkopírovat data do modulu ATO?' 'ATO?'
    if result<>1 goto protokol
endif

sendln 'xa'
wait "Aktualizovat stranky v modulu ATO [A/N] ?"
if result=1 then
    mtimeout=0
    timeout=100
    send 'A'
    wait "Data prenesena uspesne."
    if result=1 goto kopiok
endif

messagebox "Chyba pøi kopírování dat do ATO!" "Chyba"
goto protokol

:kopiok
messagebox "Data do ATO úspìšnì zkopírována" "AVV OK"

:protokol
beep 0
inputbox "Vlož øadu vozidla a potvrï OK:" "Protokol o nahrávání"
idrada="XXX"
strlen inputstr
if result>0 then
    idrada=inputstr
endif

inputbox "Vlož inv. trojèíslí vozidla bez mezer a potvrï OK:" "Protokol o nahrávání"
strlen inputstr
if result>0 then
    idlok=inputstr
endif


strmatch logdir '([A-z]:){0,1}\\'
if result<>1 then
    strinsert logdir 1 aktdir
endif
filesearch logdir 
if result=0 then
    dirnamebox 'Vyber složku pro uložení záznamu'
    if result<>1 goto konec
    logdir=inputstr
endif

expsoub=".txt"
getdate datestr "-%y%m%d"
gettime timestr "-%H%M%S"

strconcat logdir "\COM"
strconcat logdir jmport
strconcat logdir "-"
strconcat logdir idrada
strconcat logdir "_"
strconcat logdir idlok
strconcat logdir datestr
strconcat logdir timestr
strconcat logdir expsoub

logopen logdir
mpause 300 ; pauza 300 ms
sendln 'm'
pause 1
send #13
pause 1
wait 'AVV>'
if result=1 logclose

:konec
sendln 'q'

messagebox 'Nahrávání korektnì ukonèeno' 'AVV OK'

closett
